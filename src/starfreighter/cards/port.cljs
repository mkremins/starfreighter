(ns starfreighter.cards.port
  (:require [starfreighter.db :as db]
            [starfreighter.gen :as gen]
            [starfreighter.rand :as rand]
            [starfreighter.util :as util]))

(def cards [

;;; "normal" repeatable jobs, cargo buy/sell offers, etc

{:id :job-deliver-cargo
 :repeatable? true
 :prereq db/can-hold-more-cargo?
 :bind   {:cargo gen/gen-normal-delivery-job}
 :weight #(* 4 (db/open-cargo-slots %))
 :gen (fn [{{:keys [cargo]} :bound :as state}]
        (let [{dest :destination, pay-before :pay-before, stuff :name} cargo
              pay [:cash (+ pay-before (:pay-after cargo))]]
          {:type :yes-no
           :speaker (:merchant cargo)
           :text [(rand-nth ["Iâ€™d like to enlist your services"
                             "Iâ€™ve got a job for you"
                             "I have a job for you"])
                  ", Captain. Can you deliver this shipment of " stuff " to "
                  [:link [:places dest]] "? "
                  (if (pos? pay-before)
                    [(rand-nth [["Iâ€™ll pay you " pay " â€“ half now"]
                                ["Payment will be " pay " â€“ half up front"]
                                ["Youâ€™ll be paid " pay " â€“ half the pay now"]])
                     [", and " (rand-nth ["half" "the rest"])]]
                    ["Pay will be " pay ", to be paid in full"])
                  " "
                  (rand-nth [["upon successful delivery of the " stuff]
                             ["upon your arrival at " dest]
                             ["when you arrive at " dest " with the " stuff]
                             ["when you drop the " stuff " off at " dest]
                             ["when you make it to " dest]])
                  "."]
           :yes [(db/add-cargo cargo)
                 (db/earn pay-before)]
           :no identity}))}

{:id :job-deliver-passenger
 :repeatable? true
 :prereq db/can-hold-more-cargo?
 :bind   {:char gen/gen-passenger-delivery-job}
 :weight (constantly 4)
 :gen (fn [{{:keys [char]} :bound :as state}]
        (let [fare (:pay-before char)]
          {:type :yes-no
           :speaker char
           :text ["Iâ€™m in need of safe passage to "
                  [:link [:places (:destination char)]]
                  ". Can you take me there? I donâ€™t have much, but "
                  (rand-nth ["I can offer you" "Iâ€™ve saved up"]) " " [:cash fare]
                  (rand-nth [" â€“ hopefully thatâ€™ll be enough."
                             " â€“ I do hope thatâ€™s enough."
                             " â€“ that should be enough, right?"
                             " â€“ thatâ€™s about standard, right?"])]
           :yes [(db/add-cargo char)
                 (db/earn fare)]
           :no identity}))}

{:id :offer-buy-cargo
 :repeatable? true
 :bind   [[:item    db/some-sellable-cargo]
          [:buyer   db/some-trusting-merchant]
          [:import? #(contains? (:imports (db/current-place %)) (-> % :bound :item :name))]]
 :weight #(cond-> 4 (-> % :bound :import?) (* 2))
 :gen (fn [{{:keys [buyer import? item]} :bound :as state}]
        (let [stuff  (:name item)
              amount (if import? +30 +20)]
          {:type :yes-no
           :speaker buyer
           :text [(rand-nth ["Greetings" "Hello"])
                  ", Captain. Iâ€™m " (rand-nth ["in the market" "looking"])
                  " to buy some " stuff ", and it looks like youâ€™ve got some for sale. "
                  (rand-nth [["How does " [:cash amount] " sound to you?"]
                             ["I can offer you " [:cash amount] " â€“ sound like a deal?"]])]
           :yes [(db/earn amount)
                 (db/drop-cargo item)
                 (if (:counterfeit? item)
                   (db/set-next-card
                     {:type :info
                      :speaker buyer
                      :text [(rand-nth ["Heyâ€¦" "Hey, hold on a secondâ€¦" "Hey, wait a minuteâ€¦"])
                             " "
                             (rand-nth ["are you trying to pull one over on me"
                                        "just who do you think youâ€™re fooling"
                                        "whatâ€™re you trying to pull"
                                        "who do you think youâ€™re fooling"])
                             "? This " stuff " is counterfeit! I demand a refund!"]
                      :ok [(db/pay (- amount))
                           (db/add-memory buyer :tried-sell-counterfeit-goods)]})
                   (db/add-memory buyer :sold-goods))]
           :no identity}))}

{:id :offer-sell-cargo
 :repeatable? true
 :prereq (every-pred db/can-hold-more-cargo? (db/can-afford? 18))
 :bind   {:item gen/gen-goods-for-sale}
 :weight db/open-cargo-slots
 :gen (fn [{{:keys [item]} :bound :as state}]
        (let [{:keys [price seller], stuff :name} item]
          {:type :yes-no
           :speaker seller
           :text ["Looking for " stuff "? "
                  (rand-nth [[(rand-nth ["Boy howdy, have" "Have"]) " I got a "
                              (rand-nth ["" "great "]) "deal for you"]
                             "How fortunate you are"
                             "Itâ€™s your lucky day"
                             "Well, youâ€™re in luck"
                             "Well, youâ€™ve come to the right place"
                             ["What incredible " (rand-nth ["fortune" "luck"])]])
                   "! Iâ€™m currently selling " stuff " for " (rand-nth ["just" "only"]) " "
                   [:cash price] " " (rand-nth ["a pop" "per unit"])
                   (rand-nth [[" â€“ the lowest price youâ€™ll get this side of " (db/rand-destination state)]
                              [". With prices like these, Iâ€™m practically giving this " stuff " away"]])
                   (rand-nth ["." "!"])]
           :yes [(db/add-cargo item)
                 (db/pay price)
                 (db/add-memory seller :bought-goods)]
           :no identity}))}

{:id :offer-join-crew
 :repeatable? true
 :prereq (every-pred db/can-hold-more-crew? (db/can-afford? 30))
 :weight db/open-crew-slots
 :gen (fn [state]
        (let [char (gen/gen-local-character state)]
          {:type :yes-no
           :speaker char
           ;; TODO work a mention of pay into this somewhere
           :text [(rand-nth ["Excuse me, Captain. "
                             "Iâ€™m looking for work, Captain. "
                             "Youâ€™re a spacer, right? "
                             ""])
                  (rand-nth ["Any chance youâ€™d have"
                             "Do you have"
                             "Do you think there might be"
                             "Have you got"
                             "Is there by any chance"
                             "Might there be"
                             "Might you have"])
                  " "
                  (rand-nth ["a place" "a use" "room"])
                  " for "
                  (rand-nth ["" "someone like "])
                  "me in your crew? "
                  (cond
                    (db/fighter? char)
                      ["Iâ€™m not much of a deep thinker, but I can hold my own in a "
                       (rand-nth ["fight" "scrap"]) "."]
                    (db/mechanic? char)
                      ["If thereâ€™s one thing " (rand-nth ["I know" "Iâ€™m good at"])
                       ", itâ€™s fixing spaceships. I grew up around them, so Iâ€™ve been "
                       (rand-nth ["at it" "doing it" "learning" "practicing"]) " my whole life."]
                    (db/medic? char)
                      ["I donâ€™t have any formal medical training, but "
                       (rand-nth ["I know" "Iâ€™ve picked up"]) " enough to patch people up "
                       "and keep them going after a close scrape."]
                    :else
                      (rand-nth [""
                                 [(rand-nth ["I donâ€™t have any"
                                             "I donâ€™t have much in the way of"
                                             "I havenâ€™t got any"
                                             "Iâ€™ve got no"
                                             "Iâ€™ve not got much in the way of"])
                                  " special skills"
                                  (rand-nth [" or anything" " or anything like that" ""])
                                  ", but Iâ€™m a hard worker and Iâ€™m " (rand-nth ["eager" "willing"])
                                  " to learn."]]))]
           :yes [(db/add-crew char)
                 (db/pay 30)]
           :no identity}))}

;;; special offers from merchants

{:id :offer-loan
 :prereq (every-pred (complement (db/can-afford? 30)) (complement :loan-info))
 :bind   {:lender db/some-trusting-merchant}
 :weight #(util/bucket (:cash (:stats %)) [[10 4] [20 3] [30 2]])
 :gen (fn [{{:keys [lender]} :bound :as state}]
        (let [amount 40]
          {:type :yes-no
           :speaker lender
           :text [(rand-nth ["Greetings, Captain!" "Hello, Captain." "So," "So, Captain,"])
                  " I hear tell youâ€™re "
                  (rand-nth ["running a bit short on" "strapped for"])
                  " cash. I could certainly lend you, say, " [:cash amount] "â€¦ "
                  "provided you agree to pay it back promptly, plus a bit of interest. "
                  (rand-nth ["Do we have a deal?" "What do you say?"])]
           :yes [#(assoc % :loan-info
                    {:amount amount
                     :lender lender
                     :collector (gen/gen-local-character state)
                     :turn-borrowed (:turn state)})
                 (db/earn amount)]
           :no identity}))}

{:id :offer-repair-ship
 :prereq (every-pred (db/has-at-most? :ship 70) (db/can-afford? 30))
 :bind   {:merchant db/some-trusting-merchant}
 :weight #(util/bucket (:ship (:stats %)) [[20 16] [40 6] [100 4]])
 :gen (fn [{{:keys [merchant]} :bound :as state}]
        (let [price -30]
          {:type :yes-no
           :speaker merchant
           :text ["Looks like your shipâ€™s in need of some repair â€“ itâ€™s practically falling apart! "
                  "Want me to help you out with that? Itâ€™ll only run you about " [:cash price] "."]
           :yes [(db/pay 30)
                 (db/adjust-stat :ship +20)]
           :no identity}))}

{:id :offer-upgrade-crew-quarters
 :prereq (every-pred #(< (:max-crew %) 6) (db/can-afford? 50))
 :bind   {:merchant db/some-trusting-merchant}
 :weight #(if (db/can-hold-more-crew? %) 1 2)
 :gen (fn [{{:keys [merchant]} :bound :as state}]
        (let [price -50]
          {:type :yes-no
           :speaker merchant
           :text ["Your vessel looks a bit cramped, Captain. If youâ€™d like, I could modify it "
                  "to give you a little more breathing roomâ€¦ for a fair price, of course. "
                  "How does " [:cash price] " sound" (rand-nth ["" " to you"]) "?"]
           :yes [#(update % :max-crew inc)
                 (db/pay price)]
           :no identity}))}

{:id :offer-upgrade-cargo-hold
 :prereq (every-pred #(< (:max-cargo %) 12) (db/can-afford? 50))
 :bind   {:merchant db/some-trusting-merchant}
 :weight #(if (db/can-hold-more-cargo? %) 1 2)
 :gen (fn [{{:keys [merchant]} :bound :as state}]
        (let [price -50]
          {:type :yes-no
           :speaker merchant
           :text ["Youâ€™re a spacer, yes? Surely itâ€™d help your trade if you could carry more cargo at once. "
                  "How about I do you a favor and modify this bucket of bolts? For a price, of course. "
                  "Does " [:cash price] " sound good" (rand-nth ["" " to you"]) "?"]
           :yes [#(update % :max-cargo inc)
                 (db/pay price)]
           :no identity}))}

;;; requests from the crew

{:id :request-bonus
 :prereq #(db/can-afford? % (* (count (:crew %)) 5))
 :weight #(+ (util/bucket (:cash (:stats %)) [[60 1] [80 2] [100 3]])
             (util/bucket (db/avg-crew-mood %) [[40 3] [60 2] [80 1] [100 0]]))
 :gen (fn [state]
        (let [crew-size (count (:crew state))
              just-one? (= crew-size 1)]
          {:type :yes-no
           :speaker (db/some-crew state)
           ;; TODO work a mention of amount into this somewhere
           :text ["Say, Capâ€™nâ€¦ "
                  (rand-nth (if just-one? ["Iâ€™ve" "weâ€™ve" "weâ€™ve both"] ["weâ€™ve" "weâ€™ve all"]))
                  " been working pretty hard lately, and "
                  (if just-one?
                    "I was"
                    [(rand-nth ["the rest of the crew and I" "we"]) " were"])
                  " wondering if " (rand-nth ["maybe " ""]) (if just-one? "I" "we")
                  " might be due a small bonus for everything " (if just-one? "I" "we")
                  " do" (rand-nth ["." " around here."])]
           :yes [(db/pay (* 5 crew-size))
                 (db/add-whole-crew-memory :gave-bonus)]
           :no (db/add-whole-crew-memory :declined-bonus-request)}))}

{:id :request-depart
 :repeatable? true
 :prereq #(>= (count (:cargo %)) 2)
 :weight #(+ (util/bucket (count (:cargo %)) [[2 1] [3 2] [4 4] [5 6] [6 8]])
             (util/bucket (count (:recent-picks %)) [[5 1] [7 2] [100 3]]))
 :gen (fn [state]
        (let [proposed-dest (db/where-to-next state)
              speaker (db/some-crew state)]
          {:type :yes-no
           ;; TODO Making this card interruptible allows the player to "cheat" by manually initiating
           ;; travel (via the map) when it comes up, then immediately canceling the initiated travel.
           ;; This gets them a new card without imposing a penalty for rejecting the crew request.
           ;;
           ;; Practically speaking, the utility of this loophole seems pretty limited. If the crew
           ;; wants to leave, you've probably already exhausted most of your other options in port,
           ;; so this card is likely to come up again within the next few draws anyway. I'd like to
           ;; close the hole at some point, but I'm not yet sure what the solution looks like, and
           ;; it's not a priority for the time being. Still, this definitely seems like the sort of
           ;; thing that's worth noting in case it turns out to have weird repercussions somewhere
           ;; further down the line.
           :interruptible? true
           :speaker speaker
           :text [(rand-nth [["Dunno â€˜bout you, Capâ€™n, but it looks to me like the pickings "
                              "to be had round here are pretty slim."]
                             "Say, Capâ€™nâ€¦ weâ€™ve been in port a while, havenâ€™t we?"
                             "Sitting here in port is getting mighty boring, Capâ€™n."
                             "Weâ€™re all getting pretty restless, Capâ€™n."
                             "Weâ€™ve been in port a good while now."])
                  " "
                  (rand-nth ["Donâ€™tcha think itâ€™s about time" "How about" "What do you say"])
                  " we "
                  (rand-nth ["get a move on" "get going" "hit the road"
                             ["set out for " [:link [:places proposed-dest]]]
                             ["shove off for " [:link [:places proposed-dest]]]])
                  (rand-nth ["" " already"]) "?"]
           :yes (db/depart-for proposed-dest)
           :no (db/add-memory speaker :declined-depart-request)}))}

{:id :request-drinking
 :prereq (db/can-afford? 5)
 :bind   {:speaker (db/some-crew-where (db/mood-at-least? 20))}
 :weight (constantly 1)
 :gen (fn [{{:keys [speaker]} :bound :as state}]
        (let [patron (assoc (gen/gen-local-character state) :name "Rowdy Bar Patron")
              some-conscious-crew
              (db/some-crew-where (complement db/unconscious?))
              some-conscious-opponent
              #(some->> % :fight-info :opponents vals (remove db/unconscious?) seq rand-nth)
              bar-fight-deck
              [{:id :you-win
                :prereq (complement some-conscious-opponent)
                :weight (constantly 1)
                :gen (fn [state]
                       (let [last-hitter (:last-hitter (:fight-info state))]
                         {:type :info
                          :speaker last-hitter
                          :text ["Thatâ€™ll teach you to mess with " (:name last-hitter) "! "
                                 "Câ€™mon, Capâ€™n, letâ€™s get outta here."]
                          :ok [#(dissoc % :fight-info)
                               db/unset-deck
                               (db/update-all-crew
                                 (fn [crew]
                                   (if (db/has-trait? crew :unconscious)
                                     (-> crew (db/drop-trait* :unconscious)
                                              (db/add-trait* :injured))
                                     (db/add-memory* crew :won-bar-fight))))]}))}

               {:id :they-win
                :prereq (complement some-conscious-crew)
                :weight (constantly 1)
                :gen (fn [state]
                       {:type :game-over
                        :text ["You die tragically in a bar fight on " (:location state) "."]
                        :deadly? true})}

               {:id :hit-with-object
                :repeatable? true
                :bind   {:hitter some-conscious-crew
                         :target some-conscious-opponent}
                :weight (constantly 1)
                :gen (fn [{{:keys [hitter target]} :bound}]
                       {:type :info
                        :text [(:shortname hitter) " hits one of your assailants with a "
                               (rand-nth ["bottle" "chair" "pitcher" "table leg"])
                               ", knocking them to the ground. It doesnâ€™t look like theyâ€™ll be "
                               "getting up any time soon."]
                        :ok #(-> % (update-in [:fight-info :opponents (:name target)] db/add-trait* :unconscious)
                                   (assoc-in [:fight-info :last-hitter] (:name hitter)))
                        :icon "ðŸ’¥"})}

               {:id :get-hit-with-object
                :repeatable? true
                :bind   {:hitter some-conscious-opponent
                         :target some-conscious-crew}
                :weight (constantly 1)
                :gen (fn [{{:keys [hitter target]} :bound}]
                       {:type :info
                        :text ["One of your assailants hits " (:shortname target) " with a "
                               (rand-nth ["bottle" "chair" "pitcher" "table leg"])
                               ", knocking them to the ground. It doesnâ€™t look like theyâ€™ll be "
                               "getting up any time soon."]
                        :ok (db/add-trait target :unconscious)
                        :icon "ðŸ’¥"})}]
              bar-fight
              {:type :info
               :speaker patron
               :text "Canâ€™t take a hint, huh? Guess weâ€™ll just have to teach you a lesson!"
               :ok (fn [state]
                     (let [opponents (into [patron] (repeatedly 2 #(gen/gen-local-character state)))]
                       (-> state (db/set-deck bar-fight-deck)
                                 (assoc :fight-info {:opponents (zipmap (map :name opponents) opponents)}))))}
              walk-away
              {:type :info
               :text ["You gather your crew and walk out of the bar, determinedly ignoring "
                      "the look of protest on " (:shortname speaker) "â€™s face."]
               :ok (db/add-memory speaker :backed-down-from-bar-fight)}
              confrontation
              {:type :yes-no
               :speaker patron
               :text (rand-nth [["Oi! We donâ€™t take too kindly to spacers round these parts. "
                                 "Howâ€™s about you shove off afore we start doinâ€™ the shoving!"]
                                ["Spacer, eh? Donâ€™t see too many of your kind round these parts. "
                                 "Reckon you oughta be on your way now."]])
               :yes (db/set-next-card walk-away)
               :no (db/set-next-card bar-fight)}
              walk-to-bar
              {:type :yes-no
               :speaker {:name "Bartender"}
               :text "So, whatâ€™ll it be? You havinâ€™ anything?"
               :yes [(db/pay 2)
                     (db/set-next-card confrontation)]
               :no (db/set-next-card confrontation)}]
          {:type :yes-no
           :speaker speaker
           :text ["Hey, Capâ€™n â€“ have you even left the ship since we got into port? "
                  "Câ€™mon, come have a drink with us!"]
           :yes (db/set-next-card walk-to-bar)
           :no identity}))}

{:id :request-gambling
 :prereq (db/can-afford? 5)
 :bind   {:speaker (db/some-crew-where (db/mood-at-least? 20))}
 :weight (constantly 1)
 :gen (fn [{{:keys [speaker]} :bound :as state}]
        (let [dealer {:name "Dealer"}
              did-well
              ["Ooh, thatâ€™s not bad at all! You, my friend, might just be in for an exceptionally good night. "
               "Alright then, letâ€™s have another go!"]
              broke-even
              "How inconclusive! Thatâ€™s simply no fun at all â€“ best have another go!"
              did-poorly
              "Ooh, thatâ€™s too bad! Never mind that, though â€“ Iâ€™m sure your luck will turn around before long!"
              cant-afford
              "Oh, what a pity â€“ seems you canâ€™t afford to play any more. Do be sure to return when you can!"]
          (letfn [(walk-away [attempts]
                    {:type :info
                     :speaker dealer
                     :text "Walking away already? Well, I certainly hope you enjoyed yourself!"
                     ;; TODO make this outcome dynamic again
                     :ok (db/add-memory speaker :went-gambling-broke-even)})
                  (roll-the-dice [attempts]
                    {:type :info
                     :speaker speaker
                     :text "Câ€™mon, Capâ€™n â€“ letâ€™s see how we do!"
                     :icon "ðŸŽ²"
                     :ok (fn [state]
                           (let [outcome (rand-nth [0  0  0  0  0  0  0
                                                    5  5  5  5  5  5  5
                                                    10 15 20])]
                             (-> state
                                 (db/earn outcome)
                                 (db/set-next-card
                                   (if (< (+ (:cash (:stats state)) outcome) 5)
                                     (assoc (walk-away attempts) :text cant-afford)
                                     (assoc (make-wager attempts) :text
                                       (cond (< outcome 5) did-poorly
                                             (= outcome 5) broke-even
                                             (> outcome 5) did-well)))))))})
                  (make-wager [attempts]
                    {:type :yes-no
                     :speaker dealer
                     :text "Are you feeling lucky? Câ€™mon, then, step right on up to the table and place your bets."
                     :yes [(db/pay -5)
                           (db/set-next-card (roll-the-dice (inc attempts)))]
                     :no (db/set-next-card (walk-away attempts))})]
            {:type :yes-no
             :speaker speaker
             :text ["Hey, Capâ€™n â€“ as long as weâ€™re in port, Iâ€™m gonna go try my "
                    (rand-nth ["hand" "luck"]) " at some gambling. Wanna come along?"]
             :yes (db/set-next-card (make-wager 0))
             :no identity})))}

{:id :request-resign
 :prereq #(> (count (:crew %)) 1)
 :bind   {:quitter (db/some-crew-where (db/mood-at-most? 30))}
 :weight #(util/bucket (-> % :bound :quitter db/calc-mood) [[5 8] [10 6] [15 4] [20 2] [30 1]])
 :gen (fn [{{:keys [quitter]} :bound :as state}]
        (let [bonus 30
              on-resign
              [(db/add-whole-crew-memory :crewmate-resigned)
               (db/drop-crew quitter)]
              fail-to-convince
              {:type :info
               :speaker quitter
               :ok on-resign}
              next-if-no
              (if (db/can-afford? bonus)
                {:type :yes-no
                 :speaker quitter
                 :text ["Hmm. Well, if you gave me a really substantial bonus â€“ say, " [:cash bonus]
                        " â€“ I suppose thereâ€™s a chance you might be able to convince me to stay onâ€¦"]
                 :yes [(db/add-memory quitter :gave-bonus)
                       (db/pay bonus)]
                 :no (db/set-next-card
                        (assoc fail-to-convince :text
                          "Alright, then â€“ I guess itâ€™s official. I quit."))}
                (assoc fail-to-convince :text
                  ["Sorry, but I just donâ€™t think thereâ€™s anything you can offer me "
                   "that would be enough to convince me to stay."]))]
          {:type :yes-no
           :speaker quitter
           :text ["Sorry, Capâ€™n, but I think " (rand-nth ["this" (:location state)])
                  " might just be the end of the line for me. Iâ€™d like to request "
                  "your permission to resign."]
           :yes on-resign
           :no (db/set-next-card next-if-no)}))}

;;; flavor/informational

{:id :info-crew-home
 :prereq #(not (contains? (:recent-picks %) :offer-join-crew))
 :bind   {:speaker #(db/some-crew-where % (db/from? (db/current-place %)))}
 :weight (constantly 4)
 :gen (fn [{{:keys [speaker]} :bound :as state}]
        {:type :info
         :speaker speaker
         :text ["Yâ€™know, Capâ€™n, I never thought Iâ€™d say this, but itâ€™s actually "
                "kind of nice to " (rand-nth ["see" "visit"]) " "
                (rand-nth ["home" "my home planet" "my old home planet"])
                " again. Especially since this time, I know I can leave whenever I want!"]
         :ok (db/add-memory speaker :visited-home)})}

;;; followups for ongoing plotlines

{:id :collect-loan
 :prereq #(and (:loan-info %)
               (not (:collection-failed? (:loan-info %)))
               (>= (- (:turn %) (:turn-borrowed (:loan-info %))) 20))
 :weight #(util/bucket (- (:turn %) (:turn-borrowed (:loan-info %)))
            [[25 1] [30 2] [35 4] [40 8] [45 16] [50 32] [1000 100]])
 :gen (fn [state]
        (let [{:keys [collector lender]} (:loan-info state)
              pay-up
              {:type :info
               :speaker collector
               :text ["Pleasure doing business with you, Captain. "
                      (:shortname lender) " sends their regards!"]
               :ok #(dissoc % :loan-info)}
              surrender
              {:type :game-over
               :text ["Your ship is repossessed and your remaining assets seized. "
                      "Your only hope is that they are collectively worth enough "
                      "to clear the debt and keep you out of indentured servitude "
                      "to " (:name lender) "."]}
              fight-outcome
              (let [fight-score (reduce + (map #(if (db/fighter? %) 2 1) (db/crew state)))
                    enemy-fight-score (rand/rand-int* 2 5)]
                (cond
                  (> fight-score enemy-fight-score)
                    {:type :info
                     :speaker (db/some-crew-preferably state db/fighter?)
                     :text ["Yee-haw, look at â€˜em run! All you gotta do is zap the leader "
                            "and the restâ€™ll go running for the hills."]
                     :ok [#(assoc-in % [:loan-info :collection-failed?] true)
                          (db/add-whole-crew-memory :won-collector-fight)]}
                  (= fight-score enemy-fight-score)
                    {:type :info
                     :speaker collector
                     :text ["Well thenâ€¦ looks like this is my cue to exit. When next we meet, "
                            "please do try to be a bit more civil â€“ otherwise I might have to do "
                            "something weâ€™ll both regret."]
                     :ok #(assoc-in % [:loan-info :collection-failed?] true)}
                  (< fight-score enemy-fight-score)
                    {:type :game-over
                     :deadly? true
                     :text ["All at once, your chest lights up with pain, and you instinctively "
                            "gasp for air. The last thing you see before you lose consciousness is "
                            "the slight frown of disapproval on " (:name collector) "â€™s face."]}))
              fight
              {:type :info
               :text ["Youâ€™re not sure who shoots first. Your crew are quick on the draw, "
                      "but so are " (:shortname collector) "â€™s goons. Dust flies in your face "
                      "as you duck for cover, the air around you full of searing light."]
               :icon "ðŸ’¥"
               :ok [(db/set-next-card fight-outcome)
                    (db/add-memory lender :refused-repay-fought-collector)]}
              cant-afford
              {:type :yes-no
               :speaker collector
               :text ["Ah, you canâ€™t afford it? That is indeed a problem. "
                      "In that case, Iâ€™m afraid Iâ€™ll have to ask you to surrender your vessel "
                      "and submit to arrest. We must recoup our losses somehowâ€¦"]
               :yes [(db/pay 100)
                     (db/set-next-card surrender)]
               :no (db/set-next-card fight)}]
          {:type :yes-no
           :speaker collector
           :text ["Hello, Captain. Remember that money you borrowed from "
                  [:link lender]
                  (rand-nth ["" [" back on " [:link [:places (:home lender)]]]])
                  "? Well, "
                  (rand-nth ["Iâ€™m here to collect it"
                             "Iâ€™ve been sent to collect it"
                             "itâ€™s time to pay up"])
                  "! "
                  (rand-nth ["C" "Now, c"]) "an we" (rand-nth [" all" ""])
                  " agree to do this the easy way?"]
           :yes (if (db/can-afford? state 50)
                  [(db/pay 50)
                   (db/set-next-card pay-up)]
                  (db/set-next-card cant-afford))
           :no (db/set-next-card fight)}))}
])
